chaduke

high

# proveWithdrawalTransaction() fails to detect that ``L2_ORACLE.getL2Output(provenWithdrawal.l2OutputIndex).outputRoot``  might return obsolete value.

## Summary
proveWithdrawalTransaction() fails to consider the case that  ``L2_ORACLE.getL2Output(provenWithdrawal.l2OutputIndex).outputRoot might be obsolete and thus will revert always (L268) due to out of bound of array access. This  is because the output might have been deleted and ``provenWithdrawal.l2OutputIndex`` might be obsolete and out of bound. 

## Vulnerability Detail

``proveWithdrawalTransaction()`` is used to prove withdrawal transaction: 

[https://github.com/ethereum-optimism/optimism/blob/9b9f78c6613c6ee53b93ca43c71bb74479f4b975/packages/contracts-bedrock/contracts/L1/OptimismPortal.sol#L232-L308](https://github.com/ethereum-optimism/optimism/blob/9b9f78c6613c6ee53b93ca43c71bb74479f4b975/packages/contracts-bedrock/contracts/L1/OptimismPortal.sol#L232-L308)


However,  the following requirement statement is used to capture two cases as follows:

1) provenWithdrawal.timestamp == 0, this is the first time the withdrawl is proved; 
2) ``provenWithdrawal.timestamp !=0 && L2_ORACLE.getL2Output(provenWithdrawal.l2OutputIndex).outputRoot !=
                provenWithdrawal.outputRoot.``

```javascript
require(
            provenWithdrawal.timestamp == 0 ||
                L2_ORACLE.getL2Output(provenWithdrawal.l2OutputIndex).outputRoot !=
                provenWithdrawal.outputRoot,
            "OptimismPortal: withdrawal hash has already been proven"
        );
```

However,L2_ORACLE.getL2Output(provenWithdrawal.l2OutputIndex).outputRoot might have a out-bound array access because the corresponding output has been deleted by function ``deleteL2Outputs()`` (see below). Although in this case, we also need to re-prove the withdrawl, the function will revert due to out-of-bound access and thus the withdrawl cannot be reproven. 

```javascript
   function deleteL2Outputs(uint256 _l2OutputIndex) external {
        require(
            msg.sender == CHALLENGER,
            "L2OutputOracle: only the challenger address can delete outputs"
        );

        // Make sure we're not *increasing* the length of the array.
        require(
            _l2OutputIndex < l2Outputs.length,
            "L2OutputOracle: cannot delete outputs after the latest output index"
        );

        // Do not allow deleting any outputs that have already been finalized.
        require(
            block.timestamp - l2Outputs[_l2OutputIndex].timestamp < FINALIZATION_PERIOD_SECONDS,
            "L2OutputOracle: cannot delete outputs that have already been finalized"
        );

        uint256 prevNextL2OutputIndex = nextOutputIndex();

        // Use assembly to delete the array elements because Solidity doesn't allow it.
        assembly {
            sstore(l2Outputs.slot, _l2OutputIndex)
        }

        emit OutputsDeleted(prevNextL2OutputIndex, _l2OutputIndex);
    }
```


Code POC, we illustrate how the delete of some array elements might cause L268 to revert. 

1) call Setup() to create array outputs[1, 2, 3]. outputs.length = 3;
2) Call deleteAfter(2) and we have outputs[1, 2]. outputs.length = 2;
3) calling getElement(2) will revert since the element is obsolete/has been deleted and this access of an out of bound access.


```javascript
 uint256[] public outputs;

    function deleteAfter(uint256 afteridx) public {
        require(afteridx < outputs.length);
        assembly {
            sstore(outputs.slot, afteridx)
        }
    } 

    function getElement(uint i) public view returns (uint256){
        return outputs[i];
    }

    function Setup() public {
        outputs.push(1);
        outputs.push(2);
        outputs.push(3);
    }
```

## Impact
The implementation does not consider the case that an output is actually obsolete (because it is deleted), and thus needs to be re-proven. As a result, the function will always revert and thus cannot re-prove the obsolete output. 


## Code Snippet
See above

## Tool used
VScode

Manual Review

## Recommendation
We will consider the obsolete case for an output, an output is obsolete when the index is out of bound.


```diff
 function proveWithdrawalTransaction(
        Types.WithdrawalTransaction memory _tx,
        uint256 _l2OutputIndex,
        Types.OutputRootProof calldata _outputRootProof,
        bytes[] calldata _withdrawalProof
    ) external whenNotPaused {
        // Prevent users from creating a deposit transaction where this address is the message
        // sender on L2. Because this is checked here, we do not need to check again in
        // `finalizeWithdrawalTransaction`.
        require(
            _tx.target != address(this),
            "OptimismPortal: you cannot send messages to the portal contract"
        );

        // Get the output root and load onto the stack to prevent multiple mloads. This will
        // revert if there is no output root for the given block number.
        bytes32 outputRoot = L2_ORACLE.getL2Output(_l2OutputIndex).outputRoot;

        // Verify that the output root can be generated with the elements in the proof.
        require(
            outputRoot == Hashing.hashOutputRootProof(_outputRootProof),
            "OptimismPortal: invalid output root proof"
        );

        // Load the ProvenWithdrawal into memory, using the withdrawal hash as a unique identifier.
        bytes32 withdrawalHash = Hashing.hashWithdrawal(_tx);
        ProvenWithdrawal memory provenWithdrawal = provenWithdrawals[withdrawalHash];

        // We generally want to prevent users from proving the same withdrawal multiple times
        // because each successive proof will update the timestamp. A malicious user can take
        // advantage of this to prevent other users from finalizing their withdrawal. However,
        // since withdrawals are proven before an output root is finalized, we need to allow users
        // to re-prove their withdrawal only in the case that the output root for their specified
        // output index has been updated.
        require(
-            provenWithdrawal.timestamp == 0 || 
-                L2_ORACLE.getL2Output(provenWithdrawal.l2OutputIndex).outputRoot !=
-               provenWithdrawal.outputRoot,
-            "OptimismPortal: withdrawal hash has already been proven"

+            provenWithdrawal.timestamp == 0 || provenWithdrawal.l2OutputIndex >= L2_ORACLE.l2Outputs.length || 
+                L2_ORACLE.getL2Output(provenWithdrawal.l2OutputIndex).outputRoot !=
+               provenWithdrawal.outputRoot,
+            "OptimismPortal: withdrawal hash has already been proven"
        );

        // Compute the storage slot of the withdrawal hash in the L2ToL1MessagePasser contract.
        // Refer to the Solidity documentation for more information on how storage layouts are
        // computed for mappings.
        bytes32 storageKey = keccak256(
            abi.encode(
                withdrawalHash,
                uint256(0) // The withdrawals mapping is at the first slot in the layout.
            )
        );

        // Verify that the hash of this withdrawal was stored in the L2toL1MessagePasser contract
        // on L2. If this is true, under the assumption that the SecureMerkleTrie does not have
        // bugs, then we know that this withdrawal was actually triggered on L2 and can therefore
        // be relayed on L1.
        require(
            SecureMerkleTrie.verifyInclusionProof(
                abi.encode(storageKey),
                hex"01",
                _withdrawalProof,
                _outputRootProof.messagePasserStorageRoot
            ),
            "OptimismPortal: invalid withdrawal inclusion proof"
        );

        // Designate the withdrawalHash as proven by storing the `outputRoot`, `timestamp`, and
        // `l2BlockNumber` in the `provenWithdrawals` mapping. A `withdrawalHash` can only be
        // proven once unless it is submitted again with a different outputRoot.
        provenWithdrawals[withdrawalHash] = ProvenWithdrawal({
            outputRoot: outputRoot,
            timestamp: uint128(block.timestamp),
            l2OutputIndex: uint128(_l2OutputIndex)
        });

        // Emit a `WithdrawalProven` event.
        emit WithdrawalProven(withdrawalHash, _tx.sender, _tx.target);
    }
```

