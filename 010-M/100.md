obront

medium

# Wrong _minGasLimit sent from Bridge to Cross Domain Messenger

## Summary

When ETH or ERC20s are sent across the `StandardBridge`, the minimum amount of gas is not adjusted to account for the call from the `CrossDomainMessenger` to the `StandardBridge` on the other side. As a result, transactions can fail due to insufficient gas.

## Vulnerability Detail

ETH and ERC20s can be sent from the `StandardBridge` to the `CrossDomainMessenger` using the following functions:

```solidity
function _initiateBridgeETH(
    address _from,
    address _to,
    uint256 _amount,
    uint32 _minGasLimit,
    bytes memory _extraData
) internal {
    require(
        msg.value == _amount,
        "StandardBridge: bridging ETH must include sufficient ETH value"
    );

    // Emit the correct events. By default this will be _amount, but child
    // contracts may override this function in order to emit legacy events as well.
    _emitETHBridgeInitiated(_from, _to, _amount, _extraData);

    MESSENGER.sendMessage{ value: _amount }(
        address(OTHER_BRIDGE),
        abi.encodeWithSelector(
            this.finalizeBridgeETH.selector,
            _from,
            _to,
            _amount,
            _extraData
        ),
        _minGasLimit
    );
}
```

```solidity
function _initiateBridgeERC20(
    address _localToken,
    address _remoteToken,
    address _from,
    address _to,
    uint256 _amount,
    uint32 _minGasLimit,
    bytes memory _extraData
) internal {
    if (_isOptimismMintableERC20(_localToken)) {
        require(
            _isCorrectTokenPair(_localToken, _remoteToken),
            "StandardBridge: wrong remote token for Optimism Mintable ERC20 local token"
        );

        OptimismMintableERC20(_localToken).burn(_from, _amount);
    } else {
        IERC20(_localToken).safeTransferFrom(_from, address(this), _amount);
        deposits[_localToken][_remoteToken] = deposits[_localToken][_remoteToken] + _amount;
    }

    // Emit the correct events. By default this will be ERC20BridgeInitiated, but child
    // contracts may override this function in order to emit legacy events as well.
    _emitERC20BridgeInitiated(_localToken, _remoteToken, _from, _to, _amount, _extraData);

    MESSENGER.sendMessage(
        address(OTHER_BRIDGE),
        abi.encodeWithSelector(
            this.finalizeBridgeERC20.selector,
            // Because this call will be executed on the remote chain, we reverse the order of
            // the remote and local token addresses relative to their order in the
            // finalizeBridgeERC20 function.
            _remoteToken,
            _localToken,
            _from,
            _to,
            _amount,
            _extraData
        ),
        _minGasLimit
    );
}
```

In both of these cases, `MESSENGER.sendMessage()` is called with the `_minGasLimit` specified by the user.

However, once the message reaches the other side, the Cross Domain Messenger will require more than this `_minGasLimit` to successfully execute the transaction, because it will require the gas to (a) send the funds to the bridge and (b) execute the transaction.

## Impact

Bridge transactions with a precise amount of gas sent may fail due to insufficient gas.

While in most cases these transactions will be replayable by the `CrossDomainMessenger`, to ensure the safety of the bridge, it is crucial to minimize the reliance of every other part of the system working as planned.

For example, as we have shown in other issues, there are many possible attacks against replayability that might have the potential to break that functionality. It is also possible there are attacks we haven't seen that might apply.

In order to avoid building a house of cards, `_minGasLimit` should be set to the actual needed value you would expect, not a lower value with reliance on replayability to fix the miscalculation.

## Code Snippet

https://github.com/ethereum-optimism/optimism/blob/9b9f78c6613c6ee53b93ca43c71bb74479f4b975/packages/contracts-bedrock/contracts/universal/StandardBridge.sol#L361-L388

https://github.com/ethereum-optimism/optimism/blob/9b9f78c6613c6ee53b93ca43c71bb74479f4b975/packages/contracts-bedrock/contracts/universal/StandardBridge.sol#L402-L443

## Tool used

Manual Review

## Recommendation

Perform a calculation similar to `baseGas()` to determine the actual amount of gas needed for a bridge transaction, and use that adjusted value in the call to `MESSENGER.sendMessage()` rather than simply passing along the user provided `_minGasLimit`.
