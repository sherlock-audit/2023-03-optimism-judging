chaduke

high

# Logical error in finalizeWithdrawalTransaction() leads to NO REVERT during failure, and thus silent failure in most cases.

## Summary

Logical error in ``finalizeWithdrawalTransaction()`` leads to NO REVERT during failure. As a result, even when there is a failure, even when SafeCall.callWithMinGas() fails, the function will not revert, leading to a silent failure!


## Vulnerability Detail

``finalizeWithdrawalTransaction()`` is used to finalize a withdraw transaction.

[https://github.com/ethereum-optimism/optimism/blob/9b9f78c6613c6ee53b93ca43c71bb74479f4b975/packages/contracts-bedrock/contracts/L1/OptimismPortal.sol#L315-L412](https://github.com/ethereum-optimism/optimism/blob/9b9f78c6613c6ee53b93ca43c71bb74479f4b975/packages/contracts-bedrock/contracts/L1/OptimismPortal.sol#L315-L412)

There are two cases that the function needs to revert: 

1) when ``success == false`` for the low-level call

```javascript
bool success = SafeCall.callWithMinGas(_tx.target, _tx.gasLimit, _tx.value, _tx.data);
```

OR
2) ``tx.origin == Constants.ESTIMATION_ADDRESS,`` that is, the function is initiated by ``Constants.ESTIMATION_ADDRESS`` to estimate gas.

Unfortunately, the following statement uses "AND" instead of "OR", a logical error:

```javascript 
if (success == false && tx.origin == Constants.ESTIMATION_ADDRESS) {
            revert("OptimismPortal: withdrawal failed");
        }
```

As a result, the function will only REVERT when both of them are true: it is initiated by Constants.ESTIMATION_ADDRESS and the ``SafeCall.callWithMinGas(_tx.target, _tx.gasLimit, _tx.value, _tx.data)`` fails.  Surely this is not right.

As a result, in most cases, when the initiator is not Constants.ESTIMATION_ADDRESS, even when SafeCall.callWithMinGas fails, the transaction will still not revert, leading to silent failure.

## Impact
``finalizeWithdrawalTransaction()`` will have silent failures in most cases. Even when ``SafeCall.callWithMinGas(_tx.target, _tx.gasLimit, _tx.value, _tx.data)`` fails, the function will not revert and will thus have a silent failure. Users will consider the withdrawl is finalized even it is not. 

## Code Snippet
See above

## Tool used
VSCode

Manual Review

## Recommendation

Fix the logical error:

```diff
    function finalizeWithdrawalTransaction(Types.WithdrawalTransaction memory _tx)
        external
        whenNotPaused
    {
        // Make sure that the l2Sender has not yet been set. The l2Sender is set to a value other
        // than the default value when a withdrawal transaction is being finalized. This check is
        // a defacto reentrancy guard.
        require(
            l2Sender == Constants.DEFAULT_L2_SENDER,
            "OptimismPortal: can only trigger one withdrawal per transaction"
        );

        // Grab the proven withdrawal from the `provenWithdrawals` map.
        bytes32 withdrawalHash = Hashing.hashWithdrawal(_tx);
        ProvenWithdrawal memory provenWithdrawal = provenWithdrawals[withdrawalHash];

        // A withdrawal can only be finalized if it has been proven. We know that a withdrawal has
        // been proven at least once when its timestamp is non-zero. Unproven withdrawals will have
        // a timestamp of zero.
        require(
            provenWithdrawal.timestamp != 0,
            "OptimismPortal: withdrawal has not been proven yet"
        );

        // As a sanity check, we make sure that the proven withdrawal's timestamp is greater than
        // starting timestamp inside the L2OutputOracle. Not strictly necessary but extra layer of
        // safety against weird bugs in the proving step.
        require(
            provenWithdrawal.timestamp >= L2_ORACLE.startingTimestamp(),
            "OptimismPortal: withdrawal timestamp less than L2 Oracle starting timestamp"
        );

        // A proven withdrawal must wait at least the finalization period before it can be
        // finalized. This waiting period can elapse in parallel with the waiting period for the
        // output the withdrawal was proven against. In effect, this means that the minimum
        // withdrawal time is proposal submission time + finalization period.
        require(
            _isFinalizationPeriodElapsed(provenWithdrawal.timestamp),
            "OptimismPortal: proven withdrawal finalization period has not elapsed"
        );

        // Grab the OutputProposal from the L2OutputOracle, will revert if the output that
        // corresponds to the given index has not been proposed yet.
        Types.OutputProposal memory proposal = L2_ORACLE.getL2Output(
            provenWithdrawal.l2OutputIndex
        );

        // Check that the output root that was used to prove the withdrawal is the same as the
        // current output root for the given output index. An output root may change if it is
        // deleted by the challenger address and then re-proposed.
        require(
            proposal.outputRoot == provenWithdrawal.outputRoot,
            "OptimismPortal: output root proven is not the same as current output root"
        );

        // Check that the output proposal has also been finalized.
        require(
            _isFinalizationPeriodElapsed(proposal.timestamp),
            "OptimismPortal: output proposal finalization period has not elapsed"
        );

        // Check that this withdrawal has not already been finalized, this is replay protection.
        require(
            finalizedWithdrawals[withdrawalHash] == false,
            "OptimismPortal: withdrawal has already been finalized"
        );

        // Mark the withdrawal as finalized so it can't be replayed.
        finalizedWithdrawals[withdrawalHash] = true;

        // Set the l2Sender so contracts know who triggered this withdrawal on L2.
        l2Sender = _tx.sender;

        // Trigger the call to the target contract. We use a custom low level method
        // SafeCall.callWithMinGas to ensure two key properties
        //   1. Target contracts cannot force this call to run out of gas by returning a very large
        //      amount of data (and this is OK because we don't care about the returndata here).
        //   2. The amount of gas provided to the call to the target contract is at least the gas
        //      limit specified by the user. If there is not enough gas in the callframe to
        //      accomplish this, `callWithMinGas` will revert.
        // Additionally, if there is not enough gas remaining to complete the execution after the
        // call returns, this function will revert.
        bool success = SafeCall.callWithMinGas(_tx.target, _tx.gasLimit, _tx.value, _tx.data);

        // Reset the l2Sender back to the default value.
        l2Sender = Constants.DEFAULT_L2_SENDER;

        // All withdrawals are immediately finalized. Replayability can
        // be achieved through contracts built on top of this contract
        emit WithdrawalFinalized(withdrawalHash, success);

        // Reverting here is useful for determining the exact gas cost to successfully execute the
        // sub call to the target contract if the minimum gas limit specified by the user would not
        // be sufficient to execute the sub call.
-        if (success == false && tx.origin == Constants.ESTIMATION_ADDRESS) {
+        if (success == false || tx.origin == Constants.ESTIMATION_ADDRESS) {

            revert("OptimismPortal: withdrawal failed");
        }
    }
```