KingNFT

medium

# The gas estimation for calldata overhead in ````baseGas()```` is not correct

## Summary
The ````baseGas()```` is used to add proper overhead for execution of ````relayMessage()```` when called by ````finalizeWithdrawalTransaction()````. It is split into three parts: ````Dynamic overhead````, ````Calldata overhead```` and ````Constant overhead````. The issue arises on the calldata overhead, it is calculated by ````uint64(_message.length) * MIN_GAS_CALLDATA_OVERHEAD````, with ````MIN_GAS_CALLDATA_OVERHEAD = 16````. But it's not the actual case how ````_message```` affects gas cost of ````relayMessage()````.

```solidity
File: contracts\universal\CrossDomainMessenger.sol
423:     function baseGas(bytes calldata _message, uint32 _minGasLimit) public pure returns (uint64) {
424:         // We peform the following math on uint64s to avoid overflow errors. Multiplying the
425:         // by MIN_GAS_DYNAMIC_OVERHEAD_NUMERATOR would otherwise limit the _minGasLimit to
426:         // type(uint32).max / MIN_GAS_DYNAMIC_OVERHEAD_NUMERATOR ~= 4.2m.
427:         return
428:             // Dynamic overhead
429:             ((uint64(_minGasLimit) * MIN_GAS_DYNAMIC_OVERHEAD_NUMERATOR) /
430:                 MIN_GAS_DYNAMIC_OVERHEAD_DENOMINATOR) +
431:             // Calldata overhead
432:             (uint64(_message.length) * MIN_GAS_CALLDATA_OVERHEAD) + // @audit not correct
433:             // Constant overhead
434:             MIN_GAS_CONSTANT_OVERHEAD;
435:     }

```

## Vulnerability Detail
Firstly, since EIP-2028, there is indeed a gas specification for calldata
```solidity
16 gas per non-zero byte, 4 gas per zero byte
```
But this rule is only applied to top level call triggered by an EOA, which is charged as part of ````IntrinsicGas```` before execution of any contracts.
https://github.com/ethereum/go-ethereum/blob/b946b7a13b749c99979e312c83dce34cac8dd7b1/core/state_transition.go#L67
```solidity
File: core\state_transition.go
067: func IntrinsicGas(data []byte, accessList types.AccessList, isContractCreation bool, isHomestead, isEIP2028 bool, isEIP3860 bool) (uint64, error) {
...
075: 	dataLen := uint64(len(data))
076: 	// Bump the required gas by the amount of transactional data
077: 	if dataLen > 0 {
078: 		// Zero and non-zero bytes are priced differently
079: 		var nz uint64
080: 		for _, byt := range data {
081: 			if byt != 0 {
082: 				nz++
083: 			}
084: 		}
085: 		// Make sure we don't exceed uint64 for all data combinations
086: 		nonZeroGas := params.TxDataNonZeroGasFrontier
087: 		if isEIP2028 {
088: 			nonZeroGas = params.TxDataNonZeroGasEIP2028
089: 		}
090: 		if (math.MaxUint64-gas)/nonZeroGas < nz {
091: 			return 0, ErrGasUintOverflow
092: 		}
093: 		gas += nz * nonZeroGas
094: 
095: 		z := dataLen - nz
096: 		if (math.MaxUint64-gas)/params.TxDataZeroGas < z {
097: 			return 0, ErrGasUintOverflow
098: 		}
099: 		gas += z * params.TxDataZeroGas
100: 
...
113: 	return gas, nil
114: }
```
In this case, the internal call between ````finalizeWithdrawalTransaction()```` and ````relayMessage()```` doesn't have this burden.

Secondly, there are 2 other operations in ````relayMessage()```` which are subject to ````message.length````:
```solidity
(1) keccak256 hashing, 2 instances which are triggered on L308 and L317
(2) memory usage, 3 memory allocations longer than message.length which are triggered on L308, L317 and L361
```

```solidity
File: contracts\universal\CrossDomainMessenger.sol
291:     function relayMessage(
292:         uint256 _nonce,
293:         address _sender,
294:         address _target,
295:         uint256 _value,
296:         uint256 _minGasLimit,
297:         bytes calldata _message
298:     ) external payable {
299:         (, uint16 version) = Encoding.decodeVersionedNonce(_nonce);
300:         require(
301:             version < 2,
302:             "CrossDomainMessenger: only version 0 or 1 messages are supported at this time"
303:         );
304: 
305:         // If the message is version 0, then it's a migrated legacy withdrawal. We therefore need
306:         // to check that the legacy version of the message has not already been relayed.
307:         if (version == 0) {
308:             bytes32 oldHash = Hashing.hashCrossDomainMessageV0(_target, _sender, _message, _nonce);
309:             require(
310:                 successfulMessages[oldHash] == false,
311:                 "CrossDomainMessenger: legacy withdrawal already relayed"
312:             );
313:         }
314: 
315:         // We use the v1 message hash as the unique identifier for the message because it commits
316:         // to the value and minimum gas limit of the message.
317:         bytes32 versionedHash = Hashing.hashCrossDomainMessageV1(
318:             _nonce,
319:             _sender,
320:             _target,
321:             _value,
322:             _minGasLimit,
323:             _message
324:         );
325: 
326:         // Check if the reentrancy lock for the `versionedHash` is already set.
327:         if (reentrancyLocks[versionedHash]) {
328:             revert("ReentrancyGuard: reentrant call");
329:         }
330:         // Trigger the reentrancy lock for `versionedHash`
331:         reentrancyLocks[versionedHash] = true;
332: 
333:         if (_isOtherMessenger()) {
334:             // These properties should always hold when the message is first submitted (as
335:             // opposed to being replayed).
336:             assert(msg.value == _value);
337:             assert(!failedMessages[versionedHash]);
338:         } else {
339:             require(
340:                 msg.value == 0,
341:                 "CrossDomainMessenger: value must be zero unless message is from a system address"
342:             );
343: 
344:             require(
345:                 failedMessages[versionedHash],
346:                 "CrossDomainMessenger: message cannot be replayed"
347:             );
348:         }
349: 
350:         require(
351:             _isUnsafeTarget(_target) == false,
352:             "CrossDomainMessenger: cannot send message to blocked system address"
353:         );
354: 
355:         require(
356:             successfulMessages[versionedHash] == false,
357:             "CrossDomainMessenger: message has already been relayed"
358:         );
359: 
360:         xDomainMsgSender = _sender;
361:         bool success = SafeCall.callWithMinGas(_target, _minGasLimit, _value, _message);
362:         xDomainMsgSender = Constants.DEFAULT_L2_SENDER;
363: 
364:         if (success) {
365:             successfulMessages[versionedHash] = true;
366:             emit RelayedMessage(versionedHash);
367:         } else {
368:             failedMessages[versionedHash] = true;
369:             emit FailedRelayedMessage(versionedHash);
370: 
371:             // Revert in this case if the transaction was triggered by the estimation address. This
372:             // should only be possible during gas estimation or we have bigger problems. Reverting
373:             // here will make the behavior of gas estimation change such that the gas limit
374:             // computed will be the amount required to relay the message, even if that amount is
375:             // greater than the minimum gas limit specified by the user.
376:             if (tx.origin == Constants.ESTIMATION_ADDRESS) {
377:                 revert("CrossDomainMessenger: failed to relay message");
378:             }
379:         }
380: 
381:         // Clear the reentrancy lock for `versionedHash`
382:         reentrancyLocks[versionedHash] = false;
383:     }
```

The gas cost for keccak256 hashing is ````6 gas per 32 bytes````
https://github.com/ethereum/go-ethereum/blob/2adce0b06640aa665706d014a92cd06f0720dcab/params/protocol_params.go#L40
```solidity
File: params\protocol_params.go
40: 	Keccak256WordGas uint64 = 6  // Once per word of the KECCAK256 operation's data.
```

The gas cost of memory usage is not liner but quadratic versus memory size, which can be represented as 
```solidity
s = memory size
cost = s  *  params.MemoryGas / 32 + (s / 32) ^ 2 / params.QuadCoeffDiv = s * 3 / 32 + (s / 32) ^ 2 / 512
```
https://github.com/ethereum/go-ethereum/blob/2adce0b06640aa665706d014a92cd06f0720dcab/core/vm/gas_table.go#L29
```solidity
File: core\vm\gas_table.go
29: func memoryGasCost(mem *Memory, newMemSize uint64) (uint64, error) {
30: 	if newMemSize == 0 {
31: 		return 0, nil
32: 	}
33: 	// The maximum that will fit in a uint64 is max_word_count - 1. Anything above
34: 	// that will result in an overflow. Additionally, a newMemSize which results in
35: 	// a newMemSizeWords larger than 0xFFFFFFFF will cause the square operation to
36: 	// overflow. The constant 0x1FFFFFFFE0 is the highest number that can be used
37: 	// without overflowing the gas calculation.
38: 	if newMemSize > 0x1FFFFFFFE0 {
39: 		return 0, ErrGasUintOverflow
40: 	}
41: 	newMemSizeWords := toWordSize(newMemSize)
42: 	newMemSize = newMemSizeWords * 32
43: 
44: 	if newMemSize > uint64(mem.Len()) {
45: 		square := newMemSizeWords * newMemSizeWords
46: 		linCoef := newMemSizeWords * params.MemoryGas
47: 		quadCoef := square / params.QuadCoeffDiv
48: 		newTotalFee := linCoef + quadCoef
49: 
50: 		fee := newTotalFee - mem.lastGasCost
51: 		mem.lastGasCost = newTotalFee
52: 
53: 		return fee, nil
54: 	}
55: 	return 0, nil
56: }

```
As there are 3 instances and with some reserve memory, we can set
```solidity
x = message.length
s = memorySize = 4 * message.length = 4 * x
memoryCost = s * 3 / 32 + (s / 32) ^ 2 / 512
           = x * 3 / 8 + (x / 8) ^ 2 / 512
           = x * 3 / 8 + x ^ 2 / (32 * 1024)
```

Now, we can estimate the total overhead for calldata as
```solidity
hashingCost + memoryCost = x * 2 * 6 / 32 + x * 3 / 8 + x ^ 2 / (32 * 1024)
                         = x * 3 / 4 + x ^ 2 / (32 * 1024)
```

As a comparison with the current estimation, let's calculate the result for some examples:
```solidity
// 1K
currentCost = 1K * 16 = 16K 
newCost = 1K * 3 / 4 + 1K * 1K / 32 / 1024 = 800

// 10K
currentCost = 10K * 16 = 160K 
newCost = 10K * 3 / 4 + 10K * 10K / 32 / 1024 = 10,880

// 100K
currentCost = 100K * 16 = 1.6M
newCost = 100K * 3 / 4 + 100K * 100K / 32 / 1024 == 396.8K

// 1 M
currentCost = 1M * 16 = 16M 
newCost = 1M * 3 / 4 + 1M * 1M / 32 / 1024 = 3 * M / 4 + 32M ~= 32.75M
```
We can see huge difference between current estimation and the actual gas cost for calldata. Especially, when the ````_message.length```` is enough long, the current estimation is less than required gas to execute ````relayMessage()````. In other word, these withdrawal transactions can be submitted and proved, but can't be finalized.

## Impact
The calldata overhead is not calculated correctly, the difference between current estimation and the actual gas cost is huge, withdrawal transactions with long calldata might not be able to be finalized.

## Code Snippet
https://github.com/sherlock-audit/2023-03-optimism/blob/main/optimism/packages/contracts-bedrock/contracts/universal/CrossDomainMessenger.sol#L423

## Tool used

Manual Review

## Recommendation
(1) Use the correct formula  
(2) Limit the max allowed ````baseGas```` on the ````sendMessage()```` side
