obront

medium

# Setting `baseFeeMaxChangeDenominator` to 1 will break all deposits

## Summary

If `baseFeeMaxChangeDenominator` is set to `1`, then the first time that a block is skipped with no deposits, the deposit function will stop working. All depositing into the L2 will remain impossible until `baseFeeMaxChangeDenominator` is set to a value other than 1.

## Vulnerability Detail

The upgraded `ResourceMetering.sol` contract allows the admins to set the important parameters used to calculate gas costs for deposits.

One of those parameters is the `baseFeeMaxChangeDenominator`. This variable is used to determine the rate at which the last block's gas market moves the gas price, where 1 means the gas price is determined completely by the previous block, and higher numbers mean that the previous state is more highly weighted over the most recent block.

The formula can be simplified to: `baseFeeDelta = prevBaseFee * (1 + (% gas used above or below target / baseFeeMaxChangeDenominator))`

When setting this parameter, there is a check to ensure that the value is set to a positive number:
```solidity
require(_config.baseFeeMaxChangeDenominator > 0, "SystemConfig: denominator cannot be 0");
```
However, this check is not sufficient. In the case where the parameter is set to `1`, it will revert in any situation where multiple blocks are processed at once, due to the implementation of the `cdexp()` function.

First, here is how the base fee is calculated when multiple blocks are skipped:
```solidity
if (blockDiff > 1) {
    // Update the base fee by repeatedly applying the exponent 1-(1/change_denominator)
    // blockDiff - 1 times. Simulates multiple empty blocks. Clamp the resulting value
    // between min and max.
    newBaseFee = Arithmetic.clamp({
        _value: Arithmetic.cdexp({
            _coefficient: newBaseFee,
            _denominator: int256(uint256(config.baseFeeMaxChangeDenominator)),
            _exponent: int256(blockDiff - 1)
        }),
        _min: int256(uint256(config.minimumBaseFee)),
        _max: int256(uint256(config.maximumBaseFee))
    });
}
```
We call the `cdexp()` function with `newBaseFee`, `baseFeeMaxChangeDenominator`, and `blockDiff-1` as arguments.

That function is implemented as:
```solidity
function cdexp(
    int256 _coefficient,
    int256 _denominator,
    int256 _exponent
) internal pure returns (int256) {
    return
        (_coefficient *
            (FixedPointMathLib.powWad(1e18 - (1e18 / _denominator), _exponent * 1e18))) / 1e18;
}
```
If we plug in our arguments, this becomes:

`newBaseFee * (powWad(1e18 - (1e18 / baseFeeMaxChangeDenominator), (blockDiff-1 * 1e18))) / 1e18`

Simplifying further and substiting `1` in for `baseFeeMaxChangeDenominator`, we get:

`newBaseFee * powWad(0, blockDiff-1 * 1e18) / 1e18`

If we look at the implementation for `powWad()`, we see:
```solidity
function powWad(int256 x, int256 y) internal pure returns (int256) {
    // Equivalent to x to the power of y because x ** y = (e ** ln(x)) ** y = e ** (ln(x) * y)
    return expWad((lnWad(x) * y) / int256(WAD)); // Using ln(x) means x must be greater than 0.
}
```
This calls `lnWad(x)`, which would be `lnWad(0)`. Here is the start of that function:
```soliidty
function lnWad(int256 x) internal pure returns (int256 r) {
    unchecked {
        require(x > 0, "UNDEFINED");
        ...
    }
}
```
This will revert with an error message of `UNDEFINED`, and the deposit will not be able to be processed.

## Impact

If `baseFeeMaxChangeDenominator` is set to `1`, then the first time that a block is skipped with no deposits, the deposit function will become bricked. As the `blockDiff` will never go down after this point, all deposits will remain bricked until `baseFeeMaxChangeDenominator` is set to a value other than 1.

## Code Snippet

https://github.com/ethereum-optimism/optimism/blob/9b9f78c6613c6ee53b93ca43c71bb74479f4b975/packages/contracts-bedrock/contracts/L1/SystemConfig.sol#L280

https://github.com/ethereum-optimism/optimism/blob/9b9f78c6613c6ee53b93ca43c71bb74479f4b975/packages/contracts-bedrock/contracts/L1/ResourceMetering.sol#L119-L138

https://github.com/transmissions11/solmate/blob/ed67feda67b24fdeff8ad1032360f0ee6047ba0a/src/utils/FixedPointMathLib.sol#L29-L32

https://github.com/transmissions11/solmate/blob/ed67feda67b24fdeff8ad1032360f0ee6047ba0a/src/utils/FixedPointMathLib.sol#L94

## Tool used

Manual Review

## Recommendation

When setting the `baseFeeMaxChangeDenominator` parameter, ensure the value is greater than 1:
```diff
-require(_config.baseFeeMaxChangeDenominator > 0, "SystemConfig: denominator cannot be 0");
+require(_config.baseFeeMaxChangeDenominator > 1, "SystemConfig: denominator must be greater than 1");
```